#include <WiFi.h>//WiFi
#include <HTTPClient.h>//瀏覽器
#include <ArduinoJson.h>//請先安裝ArduinoJson程式庫

char ssid[] = "你的WiFi SSID"; //請修改為您連線的網路名稱
char password[] = "你的WiFi 密碼"; //請修改為您連線的網路密碼
char url[] = "http://data.moenv.gov.tw/api/v2/aqx_p_02?api_key=e8dd42e6-9b8b-43f8-991e-b3dee723a52d"; //讀取的網址及授權密碼

void setup() {
  Serial.begin(115200);
  delay(1000);
  Serial.print("開始連線到無線網路SSID:");
  Serial.println(ssid);
  //1.設定WiFi模式
  WiFi.mode(WIFI_STA);
  //2.啟動WiFi連線
  WiFi.begin(ssid, password);
  //3.檢查連線狀態
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(1000);
  }
  Serial.println("連線完成");
}

void loop() {
  //4.啟動網頁連線
  Serial.println("啟動網頁連線");
  HTTPClient http;
  http.begin(url);
  int httpCode = http.GET();
  Serial.print("httpCode=");
  Serial.println(httpCode);
  //5.檢查網頁連線是否正常
  if (httpCode == HTTP_CODE_OK) {
    //6.取得網頁內容
    String payload = http.getString();
    Serial.print("payload=");
    //7.將資料顯示在螢幕上
    Serial.println(payload);
    //JSON格式解析
    DynamicJsonDocument AQIJson(payload.length() * 2); //宣告一個JSON文件，名稱為AQIJson
    deserializeJson(AQIJson, payload);//解析網頁內容payload為JSON格式，存放在AQIJson內
    for (int i = 0; i <= AQIJson["records"].size(); i++) {
	  // 瀏覽records內的所有紀錄，直到找到site=="橋頭"
      if (AQIJson["records"][i]["site"] == "橋頭") {        
        String AQI = AQIJson["records"][i]["pm25"];
        Serial.println("橋頭PM2.5=" + AQI);
		    break;
      }
    }
  }
  http.end();
  delay(10000);
}